apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/tests/unit/schemas".toString(),
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [
                'src',
                // Also export the test util so dependencies (mainly connectionservice) can use it for testing.
                'tests/unit/src/com/google/android/libraries/car/trustagent/testutils',
            ]
            res.srcDirs = ['res']
            proto {
                srcDir java.srcDirs
            }
        }

        test {
            java.srcDirs = ['tests/unit/src']
            assets.srcDirs += ['tests/unit/schemas']
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    annotationProcessor libs.androidx.room.compiler

    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    implementation libs.gson
    implementation libs.guava
    implementation libs.javax.annotation
    implementation libs.protobuf

    implementation libs.bundles.androidx.lifecycle
    implementation libs.bundles.androidx
    implementation libs.bundles.kotlin

    implementation project(path: ":companionprotos")
    implementation project(path: ":encryptionrunner")

    kapt libs.androidx.room.compiler

    testImplementation libs.androidx.room.testing
    testImplementation libs.dexmaker
    testImplementation libs.truth
    testImplementation libs.nhaarman.mockito.kotlin
    testImplementation libs.kotlin.junit.test
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockito
    testImplementation libs.robolectric

    testImplementation libs.bundles.androidx.testing
}

protobuf {
    protoc {
        artifact = rootProject.ext.protocVersion
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
                kotlin {
                    option "lite"
                }
            }
        }
    }
}
