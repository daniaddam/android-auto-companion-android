apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            proto {
                srcDir java.srcDirs
            }
        }

        test {
            java.srcDirs = ['tests/unit/src']
            assets.srcDirs += ['tests/unit/schemas']
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation libs.bundles.androidx
    implementation libs.bundles.autovalue
    implementation libs.bundles.kotlin

    implementation libs.guava
    implementation libs.dagger
    implementation libs.protobuf
    implementation libs.javax.annotation

    implementation project(path: ":companionprotos")
    implementation project(path: ":trustagent")

    kapt "com.google.dagger:dagger-compiler:2.33"
    kapt "com.google.auto.factory:auto-factory:1.0.1"
    kapt "com.google.auto.value:auto-value:1.8"

    testImplementation libs.bundles.androidx.testing

    testImplementation libs.dexmaker
    testImplementation libs.truth
    testImplementation libs.nhaarman.mockito.kotlin
    testImplementation libs.kotlin.junit.test
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockito
    testImplementation libs.robolectric
    testImplementation libs.hamcrest
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.12'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}
