apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.13'
    }
}

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/tests/unit/schemas".toString(),
                ]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['src/com/google/android/libraries/car/communication/messagingsync/res']
            proto {
                srcDir java.srcDirs
            }
        }

        test {
            java.srcDirs = ['tests/unit/src']
            assets.srcDirs += ['tests/unit/schemas']
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation libs.bundles.androidx
    implementation libs.bundles.kotlin

    implementation libs.guava
    implementation libs.protobuf

    implementation project(path: ":trustagent")
    implementation project(path: ":communicationproto")

    testImplementation libs.bundles.androidx.testing

    testImplementation libs.dexmaker
    testImplementation libs.truth
    testImplementation libs.nhaarman.mockito.kotlin
    testImplementation libs.kotlin.junit.test
    testImplementation libs.kotlin.coroutines.test
    testImplementation libs.mockito
    testImplementation libs.robolectric
}

protobuf {
    protoc {
        artifact = rootProject.ext.protocVersion
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}
